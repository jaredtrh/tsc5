#include "imports/stdlib.fc";

forall X -> (tuple) to_tuple (X x) asm "NOP";
forall X -> tuple set_at(tuple t, X value, int index) asm "SETINDEXVAR";
forall X -> (tuple, ()) ~set_at(tuple t, X value, int index) asm "SETINDEXVAR";
(cell, (int, slice, int)) udict_rem_min(cell dict, int key_len) asm(-> 0 2 1 3) "DICTUREMMIN";

() recv_internal() impure {
    
}

(cell, tuple) push(tuple maze, cell unvis, tuple info, int i, int j, int key, int ni, int nj) inline {
    int x = maze.at(ni).at(nj);
    int nkey = key + 961 + (ni - i) * 31 + nj - j;
    if (x == "?"u) {
        nkey += 961 * 31 * 31;
    } elseif (x == "X"u) {
        nkey += 961 * 961 * 31 * 31;
    }
    int oldkey = info.at(ni).at(nj).preload_uint(30) * 961 + ni * 31 + nj;
    if (nkey < oldkey) {
        unvis~udict_delete?(40, oldkey);
        unvis~udict_set_builder(40, nkey, begin_cell());
        info~set_at(info.at(ni).set_at(begin_cell().store_uint(nkey / 961, 30).store_uint(i, 5).store_uint(j, 5).end_cell().begin_parse(), nj), ni);
    }
    return (unvis, info);
}

(int, int, int, tuple) solve(int n, int m, tuple maze) method_id {
    cell unvis = new_dict();
    tuple info = empty_tuple();
    int si = 0;
    int sj = 0;
    int i = 0;
    while (i < n) {
        tuple maze_row = maze.at(i);
        tuple info_row = empty_tuple();
        int j = 0;
        while (j < m) {
            int x = maze_row.at(j);
            if (x == "E"u) {
                si = i;
                sj = j;
                unvis~udict_set_builder(40, i * 31 + j, begin_cell());
                info_row~tpush(begin_cell().store_uint(0, 30).end_cell().begin_parse());
            } else {
                info_row~tpush(begin_cell().store_uint(961 * 961 * 961, 30).end_cell().begin_parse());
            }
            j += 1;
        }
        info~tpush(info_row);
        i += 1;
    }
    
    int i = 0;
    int j = 0;
    do {
        (int key, _, _) = unvis~udict_rem_min(40);
        (i, j) = divmod(key, 31);
        i %= 31;

        if (i > 0) {
            if (j > 0) {
                (unvis, info) = push(maze, unvis, info, i, j, key, i - 1, j - 1);
            }
            (unvis, info) = push(maze, unvis, info, i, j, key, i - 1, j);
            if (j + 1 < m) {
                (unvis, info) = push(maze, unvis, info, i, j, key, i - 1, j + 1);
            }
        }
        if (j > 0) {
            (unvis, info) = push(maze, unvis, info, i, j, key, i, j - 1);
        }
        if (j + 1 < m) {
            (unvis, info) = push(maze, unvis, info, i, j, key, i, j + 1);
        }
        if (i + 1 < n) {
            if (j > 0) {
                (unvis, info) = push(maze, unvis, info, i, j, key, i + 1, j - 1);
            }
            (unvis, info) = push(maze, unvis, info, i, j, key, i + 1, j);
            if (j + 1 < m) {
                (unvis, info) = push(maze, unvis, info, i, j, key, i + 1, j + 1);
            }
        }
    } until (maze.at(i).at(j) == "S"u);

    slice s = info.at(i).at(j);
    int key = s~load_uint(30);
    i = s~load_uint(5);
    j = s.preload_uint(5);
    
    while (maze.at(i).at(j) != "E"u) {
        maze~set_at(maze.at(i).set_at("!"u, j), i);

        slice s = info.at(i).at(j);
        s~skip_bits(30);
        i = s~load_uint(5);
        j = s.preload_uint(5);
    };

    return (key / 923521, key / 961 % 961, key % 961, maze);
}