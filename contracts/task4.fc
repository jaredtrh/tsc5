#include "imports/stdlib.fc";

forall X -> tuple set_at(tuple t, X value, int index) asm "SETINDEXVAR";
forall X -> (tuple, ()) ~set_at(tuple t, X value, int index) asm "SETINDEXVAR";
(cell, (int, slice, int)) udict_rem_min(cell dict, int key_len) asm(-> 0 2 1 3) "DICTUREMMIN";
forall X -> tuple single_tuple(X x) asm "SINGLE";

() recv_internal() impure {
    
}

(cell, tuple) push(tuple maze, cell unvis, tuple info, int i, int j, int key, int ni, int nj) inline {
    int x = maze.at(ni).at(nj);
    if (x == "?"u) {
        key += 923521;
    } elseif (x == "X"u) {
        key += 28629151;
    }
    int oldkey = info.at(ni).at(nj).at(0);
    if (key < oldkey) {
        unvis~udict_delete?(30, oldkey);
        unvis~udict_set_builder(30, key, begin_cell());
        info~set_at(info.at(ni).set_at(triple(key, i, j), nj), ni);
    }
    return (unvis, info);
}

tuple init_tuple(tuple t, int n) asm """
    DUP DEC REPEAT:<{ s1 PUSH SWAP }>
    TUPLEVAR
""";

(int, int, int, tuple) solve(int n, int m, tuple maze) method_id {
    ;;set_gas_limit(10000000000);
    tuple info = init_tuple(init_tuple(single_tuple(887503681), m), n);
    cell unvis = new_dict();
    int si = 0;
    int sj = 0;
    int i = 0;
    while (i < n) {
        tuple maze_row = maze.at(i);
        int j = 0;
        while (j < m) {
            int x = maze_row.at(j);
            if (x == "E"u) {
                si = i;
                sj = j;
                unvis~udict_set_builder(30, i * 31 + j, begin_cell());
                info~set_at(info.at(i).set_at(single(0), j), i);
            }
            j += 1;
        }
        i += 1;
    }
    
    int i = 0;
    int j = 0;
    do {
        (int key, _, _) = unvis~udict_rem_min(30);
        (i, j) = divmod(key, 31);
        i %= 31;
        key += 961;

        if (i > 0) {
            int nkey = key - 31;
            if (j > 0) {
                (unvis, info) = push(maze, unvis, info, i, j, nkey - 1, i - 1, j - 1);
            }
            (unvis, info) = push(maze, unvis, info, i, j, nkey, i - 1, j);
            if (j + 1 < m) {
                (unvis, info) = push(maze, unvis, info, i, j, nkey + 1, i - 1, j + 1);
            }
        }
        if (j > 0) {
            (unvis, info) = push(maze, unvis, info, i, j, key - 1, i, j - 1);
        }
        if (j + 1 < m) {
            (unvis, info) = push(maze, unvis, info, i, j, key + 1, i, j + 1);
        }
        if (i + 1 < n) {
            int nkey = key + 31;
            if (j > 0) {
                (unvis, info) = push(maze, unvis, info, i, j, nkey - 1, i + 1, j - 1);
            }
            (unvis, info) = push(maze, unvis, info, i, j, nkey, i + 1, j);
            if (j + 1 < m) {
                (unvis, info) = push(maze, unvis, info, i, j, nkey + 1, i + 1, j + 1);
            }
        }
    } until (maze.at(i).at(j) == "S"u);

    (int key, i, j) = info.at(i).at(j).untriple();
    key /= 961;
    
    while (maze.at(i).at(j) != "E"u) {
        maze~set_at(maze.at(i).set_at("!"u, j), i);
        (_, i, j) = info.at(i).at(j).untriple();
    };

    return (key / 29791, key / 961 % 31, key % 961, maze);
}